(* L1 Compiler
 * L1 grammar
 * Author: Kaustuv Chaudhuri <kaustuv+@cs.cmu.edu>
 * Modified: Frank Pfenning <fp@cs.cmu.edu>
 *)

%%
%header (functor BVLrValsFn (structure Token : TOKEN))

%term 
   LPAREN | RPAREN |
   LAMBDA |
   IDENT of string |
   ZERO | 
   ONE | 
   IF0 | 
   FOLD |
   NOT | SHL1 | SHR1 | SHR4 | SHR16 |
   AND | OR | XOR | PLUS |
   EOF

%nonterm 
   program of BVP.program
 | expr of BVP.expr
 | binop of BVP.binop
 | unop of BVP.unop
 | id of BVP.id

%verbose                                (* print summary of errors *)
%pos int                                (* positions *)
%start program
%eop EOF
%noshift EOF

%name BV

%%

program    : LPAREN LAMBDA LPAREN id RPAREN expr RPAREN (BVP.Lambda (id, expr))

expr       : ZERO                  (BVP.Zero)
           | ONE                   (BVP.One)
           | id                    (BVP.Id id)
           | LPAREN IF0 expr expr expr RPAREN (BVP.Ifz (expr1, expr2, expr3))
           | LPAREN FOLD expr expr LPAREN LAMBDA LPAREN id id RPAREN expr RPAREN RPAREN (BVP.Fold (expr1, expr2, id1, id2, expr3))
           | LPAREN unop expr RPAREN (BVP.Unop (unop, expr))
           | LPAREN binop expr expr RPAREN (BVP.Binop (binop, expr1, expr2))

unop       : NOT (BVP.Not)
           | SHL1 (BVP.Shl1)
           | SHR1 (BVP.Shr1)
           | SHR4 (BVP.Shr4)
           | SHR16 (BVP.Shr16)

binop      : AND (BVP.And)
           | OR (BVP.Or)
           | XOR (BVP.Xor)
           | PLUS (BVP.Plus)

id         : IDENT (Symbol.symbol IDENT)
